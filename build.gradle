plugins {
    id 'java'
    id "io.freefair.lombok" version "6.3.0"
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'maven-publish'
}

group 'net.insprill'
version '17.0.0-SNAPSHOT-' + getGitRevision()

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://jitpack.io' }
    maven { url = 'https://repo.codemc.org/repository/maven-public/' }
    maven { url = 'https://repo.essentialsx.net/releases/' }
    maven { url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
}

dependencies {
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
    compileOnly 'fr.xephi:authme:5.6.0-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.1'
    compileOnly 'net.essentialsx:EssentialsX:2.19.0'
    compileOnly 'org.jetbrains:annotations:23.0.0'
    compileOnly 'org.spigotmc:spigot-api:1.18.1-R0.1-SNAPSHOT'
    compileOnly('com.github.MilkBowl:VaultAPI:1.7') { transitive = false }
    compileOnly('com.github.MyzelYam:SuperVanish:6.1.8') { transitive = false }
    implementation 'net.insprill:XenLib:cbcdcde813'
    implementation 'org.bstats:bstats-bukkit:2.2.1'
}

shadowJar {
    archiveClassifier.set('')

    var libPackage = "net.insprill.cjm.libs"
    relocate("net.insprill.xenlib", "${libPackage}.net.insprill.xenlib")
    relocate("org.bstats", "${libPackage}.org.bstats")

    minimize()
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

publishing {
    publications {
        XenLib(MavenPublication) {
            from components.java
        }
    }
}

static def getGitRevision() {
    return 'git rev-parse --verify --short HEAD'.execute().text.trim()
}
